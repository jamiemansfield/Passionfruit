buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.net.minecrell:licenser:0.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'net.minecrell.licenser'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'me.jamiemansfield'
archivesBaseName = project.name.toLowerCase()
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:19.0'
}

license {
    header = project.file('HEADER.txt')
}

processResources {
    from 'LICENSE.txt'
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://docs.oracle.com/javase/8/docs/api/'
    )
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = 'https://github.com/jamiemansfield/Passionfruit'
                        connection = 'scm:git:https://github.com/jamiemansfield/Passionfruit.git'
                        developerConnection = 'scm:git:git@github.com:jamiemansfield/Passionfruit.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/jamiemansfield/Passionfruit/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
